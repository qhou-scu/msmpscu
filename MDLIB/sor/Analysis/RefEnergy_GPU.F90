  module RefEnergy_GPU
  !**** DESCRIPTION: _______________________________________________________________________________________
  !                  The module is to calculate the energy difference between configurations generated by MD
  !                  and a reference system. Using a reference system for basic energy could diminish the
  !                  digital errors when a system containing a lot of un-moviable atoms !
  !
  !                  DEPENDENCE____________________________________________________________________________
  !                       MD_Globle_Variables_GPU.cuf
  !
  !                  SEE ALSO____________________________________________________________________________
  !                       RefVoronoiVA_GPU.F90
  !                  ______________________________________________________________________________________
  !**** HISTORY:
  !                  version 1st 2015-06 (Hou Qing)
  !                  version 2st 2017-05 (Hou Qing)
  !
  !________________________________________________________________________________________________________


  !---  USE THE CONCERNED MODULES INCLUDING BASIC TYPE DEFINITIONS ----
    use CUDAFOR
    use MD_TYPEDEF_SimMDBox
    use MD_TYPEDEF_SimMDCtrl
    use MD_TYPEDEF_InputPaser
  !-----------------------------------------------
    implicit none

      integer::m_processid=0
      !$$--- the id of  filename get from SimMDCtrl for I/O.
      !$$    Refering the definition of f_others in SimMDCtrl.
      character(len=13),parameter, private::mp_FTAGI="&AUXF_REFEGIN"
      character(len=14),parameter, private::mp_FTAGO="&AUXF_REFEGOUT"
      character(len=256),          private::m_OUTFILE =""                ! filename of output data
      character(len=256),          private::m_OUTPATH =""                ! filename of output data

      !--- calculation control parameters
      integer, private::m_INITED = 0

      !$$--- the filename of reference configuration.
      character*256,       private::m_RefCfgFile= ""
      type(SimMDBox),      private::hm_RefSimBox                    !-- the reference box
      integer, dimension(:), allocatable, private::m_Files          !-- the io  units

      real(KINDDF), private::m_EBIN = 0.001                         !-- the size of bin ineV
      real(KINDDF), private::m_MXE  = -1.D63
      real(KINDDF), private::m_MIE  =  1.D63

      private::LoadControlParameters,    &
               PrintControlParameters
  contains
  !*********************************************************************************
  subroutine LoadControlParameters(fname, SimBox, CtrlParam)
  !***  PURPOSE:   to readin control parameters from a file
  !     INPUT:     fname: the file name
  !
  !     OUTPUT:    CtrlParam, the control parameters
  !
  use MD_Globle_Variables,only:Load_ExAnalyCtl_SimMDCtrl
  implicit none
  !--- dummy vaiables
  character*(*)  ::fname
  type(SimMDBox) ::SimBox
  type(SimMDCtrl)::CtrlParam

  !--- local variables
  integer::N, LINE, STATU
  character*256::STR
  type(InputStatements)::INPUTS

  !--------
           call Load_ExAnalyCtl_SimMDCtrl(mp_FTAGI, Fname, SimBox, CtrlParam, &
                                             OutTag=mp_FTAGO, OutFile=m_OUTFILE)
           call GetExInput_SimMDCtrl(CtrlParam, mp_FTAGI, INPUTS, STATU)

            !$$--- start interprating the statements
             call Get_InputStatements("&REFCFG", INPUTS, STR, LINE)
             if(LINE .le. 0) call Get_InputStatements("&REFCFG0", INPUTS, STR, LINE)
             if(LINE .gt. 0) then
               m_RefCfgFile = ""
               call Extract_Substr(STR,1,N,m_RefCfgFile)
             end if

            !$$--- check input consistent-----
            if(len_trim(m_RefCfgFile) .le. 0) then
                write(*,fmt="(A)")          " MDPSCU Error: reference configure for Reference Energy calculation is not given."
                write(*,fmt="(A,A,A)")      "               add the keyword in control file: &REFCFG fname "
                write(*,fmt="(' Process to be stopped')")
               stop
             end if
             call Release_InputStatements(INPUTS)

            return
    end subroutine LoadControlParameters
  !*********************************************************************************

  !**********************************************************************************
   subroutine PrintControlParameters(hFile, SimBox, CtrlParam)
  !***  PURPOSE:  to print out the control parameters for this module
  !     INPUT:    hFile, SimBox, CtrlParam
  !     OUTPUT:
   use MD_CONSTANTS
   use MD_TYPEDEF_SimMDBox
   use MD_TYPEDEF_SimMDCtrl
   implicit none
  !----   DUMMY Variables
   integer,         intent(in)::hFile
   type(SimMDBox),  intent(in)::SimBox
   type(SimMDCtrl), intent(in)::CtrlParam

  !----   Local variables

         !$$--- print out the control parameters
          write(hFile,*) "!************ RefEnergy_GPU module to be performed **********"
          write(hFile,*) "!    With the control parameters:"
          if(len_trim(m_RefCfgFile) .gt. 0) then
             write(hFile,FMT="(' !    Reference configuration to be loaded from: ', A)") m_RefCfgFile(1:len_trim(m_RefCfgFile))
          else
             write(hFile,FMT="(' !    Reference configuration is the initial configuration loaded from: ', A)") &
                                                                                 SimBox%IniConfig(1:len_trim(SimBox%IniConfig))
          end if
          write(hFile,fmt="(A)")  " !     "

          if(CtrlParam%NEEDDAMP .gt. 0) then
             write(hFile,FMT="(' !    Quickdampping to be performed for ', I7, ' time steps before RefDisplace')") CtrlParam%NEEDDAMP
          else
             write(hFile,FMT="(' !    Quickdampping to be performed before RefDisplace calculation: NO')")
          end if
          write(hFile,FMT="(' !    ')")

         return
   end subroutine PrintControlParameters
  !**********************************************************************************

  !**********************************************************************************
   subroutine PreRecord_RefEnergy(SimBox, CtrlParam)
   !***  PURPOSE:  to initialize the calculation by allocate copy the number of atom in
   !     Voronoi volume on devices to a host array AC
   use MD_Globle_Variables_GPU
   use MD_NeighborsList_GPU
   use MD_Globle_Variables,   only:CreateDataFolder_Globle_Variables
   use MD_TYPEDEF_PrintList,  only:Add_PrintProcess

   implicit none
  !----   DUMMY Variables
   type(SimMDBox) ::SimBox
   type(SimMDCtrl)::CtrlParam

  !----   Local variables
   type(SimMDCtrl)::tCtrlParam
   integer::I, IFILE
   character*256::GFILE

        !$$--- to check device version is used
         call Check_DEVICES()

        !$$---  we create a temperaral simulation box and controal parameter
         call Copy_SimMDCtrl(CtrlParam, tCtrlParam)

        !$$--- to findout the I/O unit
         IFILE = 0
         do I=1, SIZE(tCtrlParam%f_tag)
            if(tCtrlParam%f_tag(I)(1:len_trim(mp_FTAGO)) .EQ. mp_FTAGO) then
                m_OUTFILE = tCtrlParam%f_others(I)
            end if
            if(tCtrlParam%f_tag(I)(1:len_trim(mp_FTAGI)) .EQ. mp_FTAGI) then
               IFILE = I
            end if
         end do

         !$$--- check the input files
         if(IFILE .le. 0) then
            write(*,fmt="(' MDPSCU Error: the control file is missed in RefVoronoiVacancy module')")
            write(*,*) "                add the keyword in SETUP file:", mp_FTAGI
            write(*,fmt="(' Process to be stopped')")
            stop
         end if

         write(*,fmt="(A)") "Loading control data from: "//CtrlParam%f_others(IFILE)(1:len_trim(CtrlParam%f_others(IFILE)))
         if(gm_hFILELOG .gt. 0) write(gm_hFILELOG,fmt="(A)") "Loading control data from: "// &
                                                            CtrlParam%f_others(IFILE)(1:len_trim(CtrlParam%f_others(IFILE)))
         call LoadControlParameters(CtrlParam%f_others(IFILE), SimBox, CtrlParam)
         call Add_PrintProcess(PrintControlParameters)

         !$$--- to create output folder
         call GetPath(m_OUTFILE, m_OUTPATH)
         call GetFname(m_OUTFILE, m_OUTFILE)
         m_OUTPATH = m_OUTPATH(1:len_trim(m_OUTPATH))//"ENERGY/"
         call CreateDataFolder_Globle_Variables(m_OUTPATH)

         !$$--- load the reference configuration
          write(*,FMT="(' Load reference configuration...', A)")

          !$$--- get box size so on
          call CopyInformation_SimMDBox(SimBox, hm_RefSimBox)
          call ClearDataProKWD_SimMDBox(hm_RefSimBox)
          call AddDataProKWD_SimMDBox(hm_RefSimBox,"XYZCOL")
          hm_RefSimBox%NPRT = 0
          write(*,fmt="(A)") "MDPSCU Message: Load reference configuration from: "//m_RefCfgFile(1:len_trim(m_RefCfgFile))
          call Load_Config_SimMDBox(m_RefCfgFile, hm_RefSimBox)

          !$$--- to prepare the IO unit
          allocate(m_Files(CtrlParam%TOTALBOX/CtrlParam%MULTIBOX))
          do I=1, size(m_Files)
             call AvailableIOUnit(m_Files(I))
             open(m_Files(I), status='scratch')
          end do
          m_MXE = -1.D64
          m_MIE =  1.D64

          CtrlParam%NEEDPOT  = 1
          m_INITED = 1

         return
   end subroutine PreRecord_RefEnergy
  !**********************************************************************************

  !**********************************************************************************
  subroutine BefOneTest_RefEnergy(ITest, SimBox0, SimBox, CtrlParam)
  !***  PURPOSE:  conduct before one test
  !    INPUT:     ITest,      the test ID
  !               SimBox0,    the simulation box, useless here
  !               SimBox,     the simulation box array, useless here
  !               CtrlParam,  the control parameter, useless here
  !   OUTPUT:
  !
   use MD_SimboxArray_GPU
   use MD_NeighborsList_GPU
   use MD_ForceLib_Factory_GPU, only:CalEpot_ForceClass, gm_ForceClass, Init_Forcetable_Dev
    implicit none
    integer                     ::Itest
    type(SimMDBox),intent(in)   ::SimBox0
    type(SimMDBox), dimension(:)::SimBox
    type(SimMDCtrl)             ::CtrlParam
    !---
          if(iand(m_INITED, 2) .eq. 0) then
             !$$*** to initialize the GPU variable
              call Initialize_Globle_Variables_DEV(hm_RefSimBox, CtrlParam)

             !$$*** to paitition the system
             call Initialize_NeighboreList_DEV(hm_RefSimBox, CtrlParam)
             call Cal_NeighBoreList_DEV(hm_RefSimBox, CtrlParam)

             call Init_Forcetable_Dev(hm_RefSimBox, CtrlParam, gm_ForceClass)
             call CalEpot_ForceClass (hm_RefSimBox, CtrlParam, gm_ForceClass)
             call CopyOut_SimBox_DEV (hm_RefSimBox)

             !---
             m_INITED = IOR(m_INITED, 2)
          end if
      return
  end subroutine BefOneTest_RefEnergy
  !****************************************************************************************

  !****************************************************************************************
  subroutine RECORD_RefEnergy(Stamp, SimBox, CtrlParam)
  !***  DESCRIPTION: to calcualte and putout the occupation state of lattice and interstials
  !                  identified using method#1
  !
  !    INPUT:  Stamp,       the recoding stamp
  !            SimBox,      the simulation boxs
  !            CtrlParam,   the control parameters for simulation
  !
  use MD_SimboxArray_GPU,      only:CopyOut_SimBox_DEV
  use MD_ForceLib_Factory_GPU, only:CalEpot_ForceClass, gm_ForceClass
  implicit none
       !--- dummy variables
       type(MDRecordStamp) ,  intent(in):: Stamp
       type(SimMDBox),dimension(:)      :: SimBox
       type(SimMDCtrl),      intent(in) :: CtrlParam
       !--- local variables
       integer::I, NB
       real(KINDDF), dimension(:), allocatable::DE
       real(KINDDF)::MXE, MIE
       !----
           call CalEpot_ForceClass(SimBox, CtrlParam, gm_ForceClass)
           call CopyOut_SimBox_DEV( SimBox)

           NB = size(SimBox)
           allocate(DE(NB) )
           do I=1, NB
              if(SimBox(I)%NPRT .gt. hm_RefSimBox%NPRT) then
                 DE(I)  = sum(SimBox(I)%EPOT(1:hm_RefSimBox%NPRT) - hm_RefSimBox%EPOT(1:hm_RefSimBox%NPRT)) + &
                          sum(SimBox(I)%EPOT(hm_RefSimBox%NPRT+1:SimBox(I)%NPRT) )
              else
                 DE(I)  = sum(SimBox(I)%EPOT(1:SimBox(I)%NPRT) - hm_RefSimBox%EPOT(1:SimBox(I)%NPRT)) +       &
                          sum(SimBox(I)%EPOT(SimBox(I)%NPRT+1:hm_RefSimBox%NPRT) )
              end if
           end do
           MXE = maxval(DE)
           MIE = minval(DE)
           if(MXE .gt. m_MXE) m_MXE = MXE
           if(MIE .lt. m_MIE) m_MIE = MIE
           write(m_Files(Stamp%ITest), *)Stamp%ITest, Stamp%IRec(1), Stamp%ITime, Stamp%Time, &
                                         (Stamp%ITest-1)*NB+1, Stamp%ITest*NB, DE(1:size(DE))
           deallocate(DE)
          return
   end subroutine RECORD_RefEnergy
  !**********************************************************************************

  !**********************************************************************************
  subroutine AftRecord_RefEnergy(SimBox, CtrlParam)
  !***  PURPOSE:  to deallocate the memories allocated
  !    INPUT:     SimBox,    the simulation box, useless here
  !               CtrlParam, the control parameter, useless here
  !   OUTPUT:
  !
   implicit none
       type(SimMDBox) ::SimBox
       type(SimMDCtrl)::CtrlParam
   !--- local variables
       character*256::GFILE
       integer::I, J, IB0, IB1, IB, ITEST, IREC, ITIME, NT, NBIN, hFIle
       real(KINDDF)::TIME, MXE, MIE
       real(KINDDF), dimension(:), allocatable::REFE, E
       integer,      dimension(:), allocatable::HIS, ORD


           !--- save the histogram
           NT   = size(m_Files)
           MXE  = m_MXE*CP_ERGEV
           MIE  = m_MIE*CP_ERGEV
           NBIN = int((MXE - MIE)/m_EBIN) + 1

           allocate(REFE(CtrlParam%MULTIBOX*NT),ORD(CtrlParam%MULTIBOX*NT), E(NBIN), HIS(NBIN))

           do I=1, NT
              rewind(m_Files(I))
           end do

           do while(.true.)
               HIS = 0
               E   = 0.D0
               do I=1, NT
                  J = (I-1)*CtrlParam%MULTIBOX+1
                  read(m_Files(I), *, end=100) ITEST, IREC, ITIME, TIME, IB0, IB1, REFE(J:J-1+CtrlParam%MULTIBOX)
               end do
               REFE = REFE*CP_ERGEV
               !--- to calculate the histogram
               do J=1, size(REFE)
                  IB      = int((REFE(J)-MIE)/m_EBIN) + 1
                  HIS(IB) = HIS(IB) + 1
                  E(IB)   = E(IB) + REFE(J)
               end do

               do IB=1, NBIN
                  if(HIS(IB) .gt. 0) then
                     E(IB) = E(IB)/dble(HIS(IB))
                  else
                     E(IB) = MIE + dble(IB-1)*m_EBIN
                  end if
               end do
               !--- to output the histogram
               GFILE = m_OUTPATH(1:len_trim(m_OUTPATH))//m_OUTFILE(1:len_trim(m_OUTFILE))
               call STRCATI(GFILE, GFILE, "_EHIS.", IREC, 4)
               call AvailableIOUnit(hFile)
               open(hFile, File=GFILE)
               write(*,*) "MDPSCU Message: save energy histogram to "//GFILE(1:len_trim(GFILE))
               write(hFile,FMT="(A, I12)")         "&TIMESTEPS       ", ITime
               write(hFile,FMT="(A, 1PE12.5, A)")  "&TIME (ps)       ", Time

               write(hFile, fmt="(3(A16,1X), A10)") "EBINS", "Energy(eV)", "Delat-E(eV)", "Histogram"
               do J=1, NBIN
                  write(hFile, fmt="(3(1PE16.6,1X), I10)") MIE + dble(J-1)*m_EBIN, E(J), E(J)-MIE, HIS(J)
               end do
               close(hFile)

               !--- to output box energy
               call Dqsort(size(REFE), REFE, ORD)
               GFILE = m_OUTPATH(1:len_trim(m_OUTPATH))//m_OUTFILE(1:len_trim(m_OUTFILE))
               call STRCATI(GFILE, GFILE, "_E.", IREC, 4)
               call AvailableIOUnit(hFile)
               open(hFile, File=GFILE)
               write(*,*) "MDPSCU Message: save energies of boxses to "//GFILE(1:len_trim(GFILE))
               write(hFile,FMT="(A, I12)")         "&TIMESTEPS       ", ITime
               write(hFile,FMT="(A, 1PE12.5, A)")  "&TIME (ps)       ", Time

               write(hFile, fmt="(A10,1X,3(A16,1X))") "BOXID", "Energy(eV)", "Delat-E(eV)"
               do J=1, size(REFE)
                  write(hFile, fmt="(I10,1X,3(1PE16.6,1X))") ORD(J), REFE(ORD(J)), REFE(ORD(J))-MIE
               end do
               close(hFile)

           end do

  100      do I=1, NT
             close(m_Files(I), status='delete')
           end do
           deallocate(REFE,ORD, E, HIS)

           call Release_SimMDBox(hm_RefSimBox)
           if(allocated(m_Files)) deallocate(m_Files)
           m_INITED = 0
          return
  end subroutine AftRecord_RefEnergy
  !**********************************************************************************

  end module RefEnergy_GPU

