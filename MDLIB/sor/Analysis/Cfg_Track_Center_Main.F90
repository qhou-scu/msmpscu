 !**** DESCRIPTION: _______________________________________________________________________________________
 !                  The program is used to place the configuration at the center of the box,
 !                  for the convenience of visualization.
 !
 !                  The clutering results generated by Cfg_Track_SIA_GPU will be loaded for the
 !                  configurations of the clusters at difference time.
 !
 !                  SEE ALSO ____________________________________________________________________________
 !                       Cfg_Track_SIA_GPU_Main.F90
 !
 !
 !**** USAGE:       _______________________________________________________________________________________
 !
 !                  Cfg_Track_Center.exe -s "filename" -T(est) t1, t2, ts -C(fg) c1, c2, cs
 !                  where:
 !                        -s filename:          - the pathname of files storing the clustering configurations generated by
 !                                                 Cfg_Track_SIA_GPU.exe
 !                  OPTIONS
 !                        -T(est) t1, t2, ts:   - the range for the tests to be involved in analysis
 !                        -C(est) c1, c2, cs:   - the range for the configurations to be involved in analysis
 !
 !                  ______________________________________________________________________________________
 !**** HISTORY:
 !                  version 1st 2015-11 (Hou Qing, Sichuan university)
 !
 !

 module Cfg_Track_Center
 use MD_CONSTANTS
 use MD_TYPEDEF_SimMDBox
 use MD_TYPEDEF_SimMDCtrl
 use MD_Globle_Variables

 implicit none

       character*256  ::m_InPath  = ""
       character*256  ::m_OutPath = ""
       type(SimMDCtrl)::m_CtrlParam
 contains

  !**********************************************************************************
   subroutine Initialize()
   !***  PURPOSE:  to load control parameters and allocate memories needed
   !
   implicit none
  !----   DUMMY Variables

  !----   Local variables
         call ExtractCmdlineFileNames_Globle_Variables()
         call CommandlineBoxsel(m_CtrlParam)
         call GetPath(gm_cfgFileName(1), m_InPath)
         call GetParentPath(m_InPath, m_OUTPATH)
         m_OUTPATH = m_OUTPATH(1:len_trim(m_OUTPATH))//"TRACK_C/"
         call CreateDataFolder_Globle_Variables(m_OUTPATH)
         return
   end subroutine Initialize
  !**********************************************************************************

  !**********************************************************************************
   subroutine RecodCenteredCluster()
  !***  DESCRIPTION: to analysis the bond information of clusters. The bond information
  !                  is descirbed in DefectBondInfo_BCC
  !
  !    INPUT:  IB,   the box IDclusters
  !
  !    OUTPUT: BONDS,    the array describing the bonds of the clusters
  !
   implicit none
       !--- dummy variables

        !--- local variables for IO
         integer::ITEST, ICFG, hFile1, hFile2
         character*256::GFILE
         type(SimMDBox)::BOX1, BOX2
         type(MDRecordStamp)::STAMP
         real*8::CENTER1(3), CENTER2(3), BS(3), HBS(3), DIS(3)
  !-------------
        !--- start analysis

             do ITEST   = m_CtrlParam%JOBID0, m_CtrlParam%JOBID1, m_CtrlParam%JOBIDSTEP
                ICFG = m_CtrlParam%STARTCFG
                call STRCATI(GFILE, m_InPath(1:len_trim(m_InPath)), "P", 0, 4)
                call STRCATI(GFILE, GFILE, "_", ITEST, 4)
                call STRCATI(GFILE, GFILE, ".", ICFG, 4)
                write(*,*) "Load configure from ", GFILE(1:len_trim(GFILE))
                call Load_Config_SimMDBox(GFILE,BOX1)
                call CenterCluster0(BOX1)
                BS(1:3)  = BOX1%ZL(1:3)
                HBS(1:3) = 0.5D0*BS(1:3)

                CENTER1(1)  = sum(BOX1%XP(1:BOX1%NPRT, 1))/dble(BOX1%NPRT)
                CENTER1(2)  = sum(BOX1%XP(1:BOX1%NPRT, 2))/dble(BOX1%NPRT)
                CENTER1(3)  = sum(BOX1%XP(1:BOX1%NPRT, 3))/dble(BOX1%NPRT)

                DIS = - CENTER1

                BOX1%XP(1:BOX1%NPRT, 1) = BOX1%XP(1:BOX1%NPRT, 1) + DIS(1)
                BOX1%XP(1:BOX1%NPRT, 2) = BOX1%XP(1:BOX1%NPRT, 2) + DIS(2)
                BOX1%XP(1:BOX1%NPRT, 3) = BOX1%XP(1:BOX1%NPRT, 3) + DIS(3)

                call STRCATI(GFILE, m_OutPath(1:len_trim(m_OutPath)), "P", 0, 4)
                call STRCATI(GFILE, GFILE, "_", ITEST, 4)
                STAMP%ITest = ITEST
                STAMP%ITime = 0
                STAMP%ISect = 0
                STAMP%Time  = 0
                STAMP%ICfg  = ICFG
                STAMP%IRec  = ICFG
                call Putout_Instance_Config_SimMDBox(GFILE, BOX1, STAMP)

                do ICFG = m_CtrlParam%STARTCFG+m_CtrlParam%CFGSTEP,  m_CtrlParam%ENDCFG, m_CtrlParam%CFGSTEP
                   call STRCATI(GFILE, m_InPath(1:len_trim(m_InPath)), "P", 0, 4)
                   call STRCATI(GFILE, GFILE, "_", ITEST, 4)
                   call STRCATI(GFILE, GFILE, ".", ICFG, 4)
                   write(*,*) "Load configure from ", GFILE(1:len_trim(GFILE))
                   call Load_Config_SimMDBox(GFILE,BOX2)
                   call CenterCluster0(BOX2)
                   CENTER2(1)  = sum(BOX2%XP(1:BOX2%NPRT, 1))/dble(BOX2%NPRT)
                   CENTER2(2)  = sum(BOX2%XP(1:BOX2%NPRT, 2))/dble(BOX2%NPRT)
                   CENTER2(3)  = sum(BOX2%XP(1:BOX2%NPRT, 3))/dble(BOX2%NPRT)

                   if(CENTER2(1) - CENTER1(1) .GT. HBS(1))  then
                      BOX2%XP(1:BOX2%NPRT, 1) = BOX2%XP(1:BOX2%NPRT, 1) - BS(1)
                   else if(CENTER1(1) - CENTER2(1) .GT. HBS(1)) then
                      BOX2%XP(1:BOX2%NPRT, 1) = BOX2%XP(1:BOX2%NPRT, 1) + BS(1)
                   end if
                   CENTER2(1)  = sum(BOX2%XP(1:BOX2%NPRT, 1))/dble(BOX2%NPRT)
                   BOX2%XP(1:BOX2%NPRT, 1) = BOX2%XP(1:BOX2%NPRT, 1) + DIS(1)

                   if(CENTER2(2) - CENTER1(2) .GT. HBS(2))  then
                      BOX2%XP(1:BOX2%NPRT, 2) = BOX2%XP(1:BOX2%NPRT, 2) - BS(2)
                   else if(CENTER1(2) - CENTER2(2) .GT. HBS(2)) then
                      BOX2%XP(1:BOX2%NPRT, 2) = BOX2%XP(1:BOX2%NPRT, 2) + BS(2)
                   end if
                   CENTER2(2)  = sum(BOX2%XP(1:BOX2%NPRT, 2))/dble(BOX2%NPRT)
                   BOX2%XP(1:BOX2%NPRT, 2) = BOX2%XP(1:BOX2%NPRT, 2) + DIS(2)

                   if(CENTER2(3) - CENTER1(3) .GT. HBS(3))  then
                      BOX2%XP(1:BOX2%NPRT, 3) = BOX2%XP(1:BOX2%NPRT, 3) - BS(3)
                   else if(CENTER1(3) - CENTER2(3) .GT. HBS(3)) then
                      BOX2%XP(1:BOX2%NPRT, 3) = BOX2%XP(1:BOX2%NPRT, 3) + BS(3)
                   end if
                   CENTER2(3)  = sum(BOX2%XP(1:BOX2%NPRT, 3))/dble(BOX2%NPRT)
                   BOX2%XP(1:BOX2%NPRT, 3) = BOX2%XP(1:BOX2%NPRT, 3) + DIS(3)

                   call STRCATI(GFILE, m_OutPath(1:len_trim(m_OutPath)), "P", 0, 4)
                   call STRCATI(GFILE, GFILE, "_", ITEST, 4)
                   STAMP%ITest = ITEST
                   STAMP%ITime = 0
                   STAMP%ISect = 0
                   STAMP%Time  = 0
                   STAMP%ICfg  = ICFG
                   STAMP%IRec  = ICFG
                   call Putout_Instance_Config_SimMDBox(GFILE, BOX2, Stamp)
                   CENTER1 = CENTER2
                end do
             end do

             return
   end subroutine RecodCenteredCluster
  !**********************************************************************************

  !****************************************************************************
  subroutine CenterCluster0(SimBox)
  !***  PORPOSE: to make the cluster in the center of the simulation box.
  !              This routine can be used in visulazation.
  !
  !     INPUT:   SimBox
  !     OUTPUT:  DIS, the displacement of the box
  !
  IMPLICIT NONE
      !$$--- DUMMY VARIABLES
      type(SimMDBox)::SimBox

      !$$--- Local variables
      integer::I, NP
      real(KINDDF)::XMI, YMI, ZMI, BS(3), HBS(3)


       NP = SimBox%NPRT
       XMI = minval(SimBox%XP(1:NP,1))
       YMI = minval(SimBox%XP(1:NP,2))
       ZMI = minval(SimBox%XP(1:NP,3))
       BS(1:3)  = SimBox%ZL(1:3)
       HBS(1:3) = BS(1:3)*C_HALF

       DO I = 1, NP
          if(SimBox%XP(I, 1) - XMI .gt. HBS(1)) then
             SimBox%XP(I, 1) = SimBox%XP(I, 1) - BS(1)
          end if

          if(SimBox%XP(I, 2) - YMI .gt. HBS(2)) then
             SimBox%XP(I, 2) = SimBox%XP(I, 2) - BS(2)
          end if

          if(SimBox%XP(I, 3) - ZMI .gt. HBS(3)) then
             SimBox%XP(I, 3) = SimBox%XP(I, 3) - BS(3)
          end if
       END DO

       return
  end subroutine CenterCluster0
  !****************************************************************************

 end module Cfg_Track_Center


 !**********************************************************************************
 Program Cfg_Track_Center_Main
 use Cfg_Track_Center
 implicit none


       call Initialize()
       call RecodCenteredCluster()

       stop
 End program Cfg_Track_Center_Main
